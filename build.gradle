plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'com.github.node-gradle.node' version '7.0.1'
}

group = 'org.in.tiaa'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // Apache POI dependencies
    implementation 'org.apache.poi:poi:5.2.3'
    implementation 'org.apache.poi:poi-ooxml:5.2.3'

    // Optional: Add this if you need additional POI features
    implementation 'org.apache.poi:poi-ooxml-full:5.2.3'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
    sourceResources sourceSets.main
    jvmArgs = ["-XX:+AllowRedefinitionToAddDeleteMethods"]
}

node {
    version = '18.12.1'
    download = true
    nodeProjectDir = file("${project.projectDir}/src/main/frontend")
}

def reactAppDir = "$projectDir/src/main/frontend"

task installReactDependencies(type: NpmTask) {
    workingDir = file(reactAppDir)
    args = ['install']
}

task buildReact(type: NpmTask) {
    workingDir = file(reactAppDir)
    args = ['run', 'build']
    dependsOn installReactDependencies
}

task copyReactBuildFiles(type: Copy) {
    from "$reactAppDir/build"
    into "$projectDir/src/main/resources/static"
    dependsOn buildReact
}

processResources.dependsOn copyReactBuildFiles

test {
    useJUnitPlatform()
}